plugins {
    id 'signing'
    id 'eclipse'
	id "net.galacticraft.addon"
    id "io.freefair.lombok" version "6.3.0"
}

apply from: 'gradle/artifact.gradle'

version = "${mod_version}"
group = mod_base_package
archivesBaseName = 'GalacticTweaks'

galacticraft {
	latestRelease()
	//latestSnapshot()
}

minecraft {

    mappings channel: mapping_channel, version: mapping_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run/client')

            //property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

repositories {
	maven { url "https://www.cursemaven.com" }
	maven { url "https://dvs1.progwml6.com/files/maven" }
}

dependencies {
	minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

	compileOnly "mezz.jei:jei_${minecraft_version}:4.16.1.302:api"
	runtimeOnly "mezz.jei:jei_${minecraft_version}:4.16.1.302"
	
	compileOnly fg.deobf("curse.maven:asmodeuscore-0.0.31-303101:3873588")
	compileOnly fg.deobf("curse.maven:galaxyspace-2.0.19-238770:3873609")
	compileOnly fg.deobf("curse.maven:extraplanets-0.7.5-241291:4366814")
	compileOnly fg.deobf("curse.maven:mjrlegendslib-1.2.1-284331:3344068")
}

tasks.withType(Copy).all {
	duplicatesStrategy 'EXCLUDE'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", "${minecraft_version}"
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'assets/galactictweaks/patchouli_books/guide/book.json'
        expand 'version':project.version, 'mcversion':"${minecraft_version}"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'assets/galactictweaks/patchouli_books/guide/book.json'
    }
    rename '(.+_at.cfg)', '$1'
}

compileJava {
    options.encoding = 'UTF-8'
}

jar {
	manifest {
		attributes(
			'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
			'Timestamp': System.currentTimeMillis(),
			'Implementation-Title': project.archivesBaseName,
			'Implementation-Version': version,
			'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
			'Built-On': "${project.minecraft_version}-${project.forge_version}",
			'FMLAT': "accesstransformer.cfg"
		)
	}
}
