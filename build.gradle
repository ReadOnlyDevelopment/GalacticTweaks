buildscript {
	repositories {
		maven {
			name = "gradle"
			url = "https://plugins.gradle.org/m2/"
		}
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'net.ltgt.apt' version '0.10'
	id 'eclipse'
	id "jacoco"
}

apply plugin: "java"
apply plugin: "org.sonarqube"
apply plugin: 'net.minecraftforge.gradle.forge'


// ==========================================
//            Load Secrets
// ==========================================
apply from: "https://raw.githubusercontent.com/ReadOnly-Mods/Misc/master/Gradle/secret.gradle"
loadSecrets()
// ==========================================


version = "${versionMajor}.${versionMinor}.${versionRev}"
group = "${group}"
archivesBaseName = "${name}"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
	sourceCompatibility = targetCompatibility = '1.8'
}

// ==========================================
//            Load GC Dev Files
// ==========================================
project.ext {
	GCBuild = "261"
	GCVersion = "1.12.2-4.0.2.${GCBuild}"
}
apply from: "https://raw.githubusercontent.com/ReadOnly-Mods/Misc/master/Gradle/gcdevfiles.gradle"
// ==========================================

sonarqube {
	properties {
		property 'sonar.GalacticTweaks', 'GalacticTweaks SonarQube Scanner'
	}
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

repositories {
	maven {
		// Location of CurseAPI Files
		url "https://jitpack.io"
	}
	mavenCentral()
	maven {
		name 'MJRLegends'
		url = "https://maven.mjrlegends.com/"
	}
	maven {
		name = 'curseforge'
		url = 'https://minecraft.curseforge.com/api/maven/'
	}
	maven {
		name 'DVS1 Maven FS'
		url 'http://dvs1.progwml6.com/files/maven'
	}
	maven {
		name 'IC2'
		url = "http://maven.ic2.player.to"
	}
	maven {
		url = "https://www.cursemaven.com"
	}

}

dependencies {

	compile("com.github.TheRandomLabs:CurseAPI:master-SNAPSHOT"){	
		exclude group: 'com.google.guava', module: 'guava'	
		}
		
	compile "org.apache.logging.log4j:log4j-core:2.13.3"
	compile "org.apache.logging.log4j:log4j-slf4j18-impl:2.13.3"

	compile "org.projectlombok:lombok:1.18.12"
	apt "org.projectlombok:lombok:1.18.12"

	compile "industrial-craft:industrialcraft-2-2.8.99:ex112:api"

	// LOAD GC ADDONS
	compile "com.mjr.planetprogression:PlanetProgression:1.12.2-0.4.5:api"
	compile "curse.maven:AsmodeusCore-Dev:3063158"
	compile "curse.maven:GSMod-0.0.14:3015062"
	compile "curse.maven:ExtraPlanets-1.7.0:3031689"
	runtime "curse.maven:MJRLib-1.2.0:3029769"
	compile "industrial-craft:industrialcraft-2-2.8.99:ex112:api"
	runtime "mezz.jei:jei_1.12.2:4.15.0.297"

}

minecraft {
	version = "${mc_version}-${forge_version}"
	runDir = "run"
	mappings = "${mcp}"

	replace '@VERSION@', project.version
	replace '@MCVERSION@', project.minecraft.version
	replace '@FINGERPRINT@', project.findProperty('signSHA1')
	replaceIn "Ref.java"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
 	    attributes 'FMLAT': 'galactictweaks_at.cfg'
 	}
 }

jar {
	manifest {
		mainAttributes(
				'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
				'Timestamp': System.currentTimeMillis(),
				'Implementation-Title': project.archivesBaseName,
				'Implementation-Version': version,
				'Built-On-Java': "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
				'Built-On': "${project.mc_version}-${project.forge_version}"
				)
	}
}

task signJar(type: SignJar, dependsOn: reobfJar) {
	onlyIf {
		project.hasProperty('keyStore')
	}

	inputFile = jar.archivePath
	outputFile = jar.archivePath

	keyStore = findProperty('keyStore')
	alias = findProperty('keyStoreAlias')
	storePass = findProperty('keyStorePass')
	keyPass = findProperty('keyStoreKeyPass')
}

curseforge {
	if (secret.containsKey('curseapi')) {
		apiKey = secret.curseapi
        project {
            id = "359766"
            changelog = file("CHANGELOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion mc_version

            mainArtifact(jar) {
                displayName = "GalacticTweaks-" + mc_version + "-" + version
            }
        }
	}
}

build.dependsOn signJar
tasks.curseforge.dependsOn(build)